buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'webchat'
version '1.0'

mainClassName = 'angular.UI'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.cloud:spring-cloud-starter-oauth2')
    runtime('org.springframework.boot:spring-boot-devtools')
    runtime('com.h2database:h2')
    providedRuntime('org.springframework.boot:spring-boot-starter-tomcat')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

ext {
    springCloudVersion = 'Finchley.RELEASE'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

def webappDir = "$projectDir/src/main/webapp"

task installAngular(type: Exec) {
    workingDir webappDir
    inputs.dir webappDir
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }
}

task buildWebApp(type: Exec) {
    dependsOn installAngular

    workingDir webappDir
    inputs.dir webappDir

    // Add task to the standard build group
    group = BasePlugin.BUILD_GROUP

    //on windows:
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
        commandLine "ng.cmd", "build", "--prod"
    } else {
        commandLine "ng", "build", "--prod"
    }

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method stopTomcat.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

jar {
    dependsOn buildWebApp
    sourceSets {
        main {
            resources {
                srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
            }
        }
    }
}

bootJar {
    launchScript()
}

war {
    dependsOn buildWebApp
    webAppDirName = "$webappDir/dist/static"
}